
ShapeArguments -> {
    $isSealed = \array_pop($children);

    return new Node\Stmt\Shape\Arguments($children, $isSealed);
}
  : ::T_BRACE_OPEN::
      ShapeArgument()? (::T_COMMA:: ShapeArgument())* ::T_COMMA::?
      IsSealed() ::T_COMMA::?
    ::T_BRACE_CLOSE::
  ;

IsSealed -> { return $children === []; }
  : <T_ELLIPSIS>?
  ;

ShapeArgument
  : OptionalNamedShapeArgument()
  | NamedShapeArgument()
  | AnonymousShapeArgument()
  ;

OptionalNamedShapeArgument -> {
    return new Node\Stmt\Shape\Argument(
        $children[1],
        $children[0]->getValue(),
        true,
    );
}
  : ShapeKey() ::T_NULLABLE:: ::T_COLON:: ShapeValue()
  ;

NamedShapeArgument -> {
    return new Node\Stmt\Shape\Argument(
        $children[1],
        $children[0]->getValue(),
    );
}
  : ShapeKey() ::T_COLON:: ShapeValue()
  ;

AnonymousShapeArgument -> {
    return new Node\Stmt\Shape\Argument(
        $children[0],
    );
}
  : ShapeValue()
  ;

ShapeKey
  : <T_NAME>
  | <T_INT_LITERAL>
  | <T_BOOL_LITERAL>
  | <T_NULL_LITERAL>
  ;

ShapeValue
  : Statement()
  ;

