
ShapeArguments -> {
    $isSealed = \array_pop($children);

    return new Node\Stmt\Shape\Arguments($children, $isSealed);
}
  : ::T_BRACE_OPEN::
      ShapeArgument()? (::T_COMMA:: ShapeArgument())* ::T_COMMA::?
      IsSealed() ::T_COMMA::?
    ::T_BRACE_CLOSE::
  ;

IsSealed -> { return $children === []; }
  : <T_ELLIPSIS>?
  ;

ShapeArgument
  : OptionalNamedShapeArgument()
  | NamedShapeArgument()
  | AnonymousShapeArgument()
  ;

OptionalNamedShapeArgument -> {
    return new Node\Stmt\Shape\Argument(
        $children[1],
        $children[0],
        true,
    );
}
  : ShapeKey() ::T_NULLABLE:: ::T_COLON:: ShapeValue()
  ;

NamedShapeArgument -> {
    return new Node\Stmt\Shape\Argument(
        $children[1],
        $children[0],
    );
}
  : ShapeKey() ::T_COLON:: ShapeValue()
  ;

AnonymousShapeArgument -> {
    return new Node\Stmt\Shape\Argument(
        $children[0],
    );
}
  : ShapeValue()
  ;

ShapeKey -> {
    return match(true) {
        $children instanceof Node\Literal\StringLiteralStmt => $children,
        $children instanceof Node\Literal\IntLiteralStmt,
        $children instanceof Node\Literal\BoolLiteralStmt,
        $children instanceof Node\Literal\NullLiteralStmt,
            => new Node\Literal\StringLiteralStmt($children->raw),
        default => new Node\Literal\StringLiteralStmt($children->getValue()),
    };
}
  : <T_NAME>
  | IntLiteral()
  | BoolLiteral()
  | NullLiteral()
  | StringLiteral()
  ;

ShapeValue
  : Statement()
  ;

