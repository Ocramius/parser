
/**
 * -----------------------------------------------------------------------------
 *  Language Syntax Summary
 * -----------------------------------------------------------------------------
 */

%include grammar/lexemes            // Lexeme/Token definitions
%include grammar/literals           // Literal definitions
%include grammar/common             // Common utils
%include grammar/template-params    // Type's template params
%include grammar/shape-fields       // Type's shape fields
%include grammar/callable           // Callable/function definitions
%include grammar/named-type         // Named type definition containing
                                    // template params and shape fields

%pragma root Statement

/**
 * -----------------------------------------------------------------------------
 *  Type Statement
 * -----------------------------------------------------------------------------
 *
 *  Constant references to external definitions or
 *  describe the type in some way.
 *
 */

#TypeStatement
  : LogicalTypeStatement()
  ;

/**
 * -----------------------------------------------------------------------------
 *  Logical Statements
 * -----------------------------------------------------------------------------
 *
 *  Logical statements denote union or intersection types, like:
 *  - A | B | C - for union type references.
 *  - A & B & C - for intersection type references.
 *
 */

LogicalTypeStatement
  : UnionTypeStatement()
  ;

UnionTypeStatement -> {
    if (\count($children) === 2) {
        return new Node\Type\UnionTypeNode($children[0], $children[1]);
    }

    return $children;
}
  : IntersectionTypeStatement() (::T_OR:: UnionTypeStatement())?
  ;

IntersectionTypeStatement -> {
    if (\count($children) === 2) {
        return new Node\Type\IntersectionTypeNode($children[0], $children[1]);
    }

    return $children;
}
  : UnaryTypeStatement() (::T_AMP:: IntersectionTypeStatement())?
  ;

/**
 * -----------------------------------------------------------------------------
 *  Unary Statements
 * -----------------------------------------------------------------------------
 *
 *  Unary statements denote terminal types, like:
 *  - A - for type reference.
 *  - ?A - for nullable type reference.
 *  - 'some' - for string literal reference.
 *  - etc...
 *
 */

UnaryTypeStatement
  : PrefixedNullableTypeStatement()
  ;

// stmt = ?Type
PrefixedNullableTypeStatement -> {
    if (\is_array($children)) {
        return new Node\Type\NullableTypeNode($children[1]);
    }

    return $children;
}
  : <T_NULLABLE> TypesListStatement()
  | SuffixedNullableTypeStatement()
  ;

// stmt = Type?
SuffixedNullableTypeStatement -> {
    if (\count($children) > 1) {
        $result = new Node\Type\NullableTypeNode($children[0]);
        $result->offset = $children[1]->getOffset();

        return $result;
    }

    return $children[0];
}
  : TypesListStatement() <T_NULLABLE>?
  ;

TypesListStatement -> {
    $statement = \array_shift($children);

    for ($i = 0, $length = \count($children); $i < $length; ++$i) {
        $statement = new Node\Type\TypesListNode($statement);
        $statement->offset = $children[$i]->getOffset();
    }

    return $statement;
}
  : PrimaryTypeStatement() (
      <T_SQUARE_BRACKET_OPEN>
      ::T_SQUARE_BRACKET_CLOSE::
    )*
  ;

// Primary

PrimaryTypeStatement
  : ::T_PARENTHESIS_OPEN:: TypeStatement() ::T_PARENTHESIS_CLOSE::
  | Literal()
  | CallableTypeStmt()
  | NamedTypeStmt()
  ;
