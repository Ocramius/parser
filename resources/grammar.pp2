
/**
 * -----------------------------------------------------------------------------
 *  Language Syntax Summary
 * -----------------------------------------------------------------------------
 */

%include grammar/lexemes
%include grammar/literals
%include grammar/template
%include grammar/shape
%include grammar/common
%include grammar/callable
%include grammar/typedef

%pragma root Statement

/**
 * -----------------------------------------------------------------------------
 *  Constant Statement
 * -----------------------------------------------------------------------------
 *
 *  A constant statement can be evaluated during translation rather than
 *  runtime, and accordingly may be used in any place that a constant may be.
 *
 */

Statement
  : BinaryStatement()
  ;

// Binary statements/expressions

BinaryStatement
  : UnionTypeStatement()
  ;

UnionTypeStatement -> {
    if (\count($children) === 2) {
        return new Node\Stmt\UnionTypeNode($children[0], $children[1]);
    }

    return $children;
}
  : IntersectionTypeStatement() (::T_OR:: UnionTypeStatement())?
  ;

IntersectionTypeStatement -> {
    if (\count($children) === 2) {
        return new Node\Stmt\IntersectionTypeNode($children[0], $children[1]);
    }

    return $children;
}
  : UnaryStatement() (::T_AND:: IntersectionTypeStatement())?
  ;

// Unary statements/expressions

UnaryStatement
  : NullableTypeStatement()
  ;

// stmt = ?Type
//  - or
// stmt = Type?
NullableTypeStatement -> {
    if (\count($children) > 1) {
        $statement = $children[0] instanceof Node\Stmt\Statement
            ? $children[0]
            : $children[1]
            ;

        return new Node\Stmt\NullableTypeNode($statement);
    }

    return $children[0];
}
  : <T_NULLABLE> TypesListStatement()
  | TypesListStatement() <T_NULLABLE>?
  ;

TypesListStatement -> {
    if (\count($children) > 1) {
        return new Node\Stmt\TypesListNode($children[0]);
    }

    return $children;
}
  : PrimaryStatement() (
      <T_SQUARE_BRACKET_OPEN>
      ::T_SQUARE_BRACKET_CLOSE::
    )?
  ;

// Primary

PrimaryStatement
  : ::T_PARENTHESIS_OPEN:: Statement() ::T_PARENTHESIS_CLOSE::
  | Literal()
  | CallableTypeStmt()
  | NamedTypeStmt()
  ;



